buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }
    dependencies {
        classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.17")
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'org.springframework.boot' version '2.4.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.bmuschko.docker-spring-boot-application' version '6.7.0'
    id 'nu.studer.jooq' version '5.2.1'
}

// see https://stackoverflow.com/questions/32352816/what-the-difference-in-applying-gradle-plugin
apply plugin: 'com.gorylenko.gradle-git-properties'

group = 'com.bufferstack'
version = '1.2'
sourceCompatibility = '11'

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/test-intg/java')
        }
        resources.srcDir file('src/test-intg/resources')
    }
}

repositories {
    mavenCentral()
}

ext {
    jacksonVersion = "2.12.3"
    postgresqlVersion = "42.2.14"
    testcontainersVersion = "1.15.3"
}

configurations {
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude group: 'ch.qos.logback', module: 'logback-classic'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'

    implementation 'org.springdoc:springdoc-openapi-ui:1.5.2'

    // Note: ZooKeeper 3.4.x is now at end-of-life. Consequently, the latest versions of Curator have removed
    // support for it. If you wish to use Curator with ZooKeeper 3.4.x you should pin to version 4.2.x of Curator.
    // Curator 4.2.x supports ZooKeeper 3.4.x ensembles in a soft-compatibility mode. To use this mode you must exclude
    // ZooKeeper when adding Curator to your dependency management tool.
    // source: https://curator.apache.org/zk-compatibility-34.html
    implementation ('org.apache.zookeeper:zookeeper:3.4.10') {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
    implementation 'org.apache.curator:curator-framework:4.2.0'
    implementation ('org.apache.curator:curator-recipes:4.2.0') {
        exclude group: 'org.apache.zookeeper', module: 'zookeeper'
    }

    implementation 'org.zalando:problem-spring-web:0.23.0'
    implementation 'com.evanlennick:retry4j:0.15.0'

    implementation "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jacksonVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-guava:${jacksonVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}"
    implementation "com.fasterxml.jackson.module:jackson-module-parameter-names:${jacksonVersion}"

    implementation 'io.micrometer:micrometer-registry-prometheus'

    runtimeOnly 'org.postgresql:postgresql'
    jooqGenerator 'com.h2database:h2:1.4.200'
    jooqGenerator 'org.jooq:jooq-meta:3.14.11'
    jooqGenerator 'org.jooq:jooq-meta-extensions:3.14.11'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

test {
    useJUnitPlatform()
}

jooq {
    version = '3.14.11'  // default (can be omitted)
    edition = nu.studer.gradle.jooq.JooqEdition.OSS  // default (can be omitted)

    configurations {
        main {  // name of the jOOQ configuration
            generateSchemaSourceOnCompilation = true  // default (can be omitted)

            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.h2.Driver'
                    url = 'jdbc:h2:~/test-gradle'
                    user = 'sa'
                    password = ''
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        //name = 'org.jooq.meta.h2.H2Database'
                        name = 'org.jooq.meta.extensions.ddl.DDLDatabase'
                        includes = '.*'
                        properties {
                            property {
                                key = 'scripts'
                                value = 'src/main/resources/db.sql'
                            }
                        }
                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'com.bufferstack.tinuyrl.jooq'
                        directory = 'build/generated-src/jooq/main'  // default (can be omitted)
                    }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}


docker {
    springBootApplication {
        baseImage = 'adoptopenjdk/openjdk11:jdk-11.0.2.9-alpine-slim'
        images = ["bufferstack/tinyurl:${version}"]
        jvmArgs = []
    }
}

// Configuring the jOOQ generation task to participate in incremental builds
tasks.named('generateJooq').configure { allInputsDeclared = true }

gitProperties {
    gitRepositoryRoot = new File("${project.rootDir}")
}
